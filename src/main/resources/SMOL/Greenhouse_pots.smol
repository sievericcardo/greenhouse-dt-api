
class PotModel extends BaseModel()
    override Unit adaptAddition()
        List<Pot> newPots = construct("
            PREFIX ast: <http://www.smolang.org/greenhouseDT#>
            SELECT ?potId {
                ?x a ast:Pot ;
                    ast:potId ?potId .
                FILTER NOT EXISTS {
                    ?y a prog:Pot ;
                        prog:Pot_potId ?potId ; .
                }
            }");

        if newPots != null then
            print("RECONFIG> New Pot(s) detected: repairing the model");

            while newPots != null do
                Pot newPot = newPots.content;
                List<Pot> lx = newPots;
                newPots = newPots.next;
                destroy(lx);

                print("RECONFIG> New pot detected: ");
                print("          Pot Id: " ++ newPot.potId);

                newPot.moistureSensor = newPot.getMoistureSensor();
                newPot.nutrientSensor = newPot.getNutrientSensor();
                newPot.pump = newPot.getPump();
            end

            print("RECONFIG> Pot(s) added");
        end
    end

    override Unit adaptRemoval()
        List<Pot> wrongPots = access("
            PREFIX ast: <http://www.smolang.org/greenhouseDT#>
            SELECT DISTINCT ?obj {
                ?obj a prog:Pot ;
                    prog:Pot_potId ?potId .
                FILTER NOT EXISTS {
                    ?y a ast:Pot ;
                        ast:potId ?potId .
                }
            }");

        if wrongPots != null then
            print("RECONFIG> Misconfigured Pot(s) detected: repairing the model");

            while wrongPots != null do
                Pot wrongPot = wrongPots.content;
                List<Pot> lx = wrongPots;
                wrongPots = wrongPots.next;

                print("RECONFIG> Misconfigured pot to remove: ");
                print("          Pot Id: " ++ wrongPot.potId);

                destroy(wrongPot);
                destroy(lx);
            end

            print("RECONFIG> Pot(s) removed");
        end
    end


    override Unit adaptDefect()
        print("RECONFIG> Checking for defects in the Pot(s)");
        // this.checkPumpChanged();
        // this.checkPlantChanged();
        print("RECONFIG> Defects checked for Pot(s)");
    end

    /* Method to check if the pump of the Pot has changed */
    Unit checkPumpChanged()

        List<PotDefectPump> changedPots = construct("
            PREFIX ast: <http://www.smolang.org/greenhouseDT#>
            SELECT DISTINCT ?obj ?pumpIdNew {
                ?obj a prog:Pot ;
                    prog:Pot_shelfFloor ?shelfFloor ;
                    prog:Pot_potPosition ?potPosition ;
                    prog:Pot_pumpId ?pumpId ;
                    prog:Pot_plantId ?plantId .
                ?y a ast:Pot ;
                    ast:shelfFloor ?shelfFloor ;
                    ast:potPosition ?potPosition ;
                    ast:wateredBy ?pump ;
                    ast:hasPlant ?plant .
                ?pump a ast:Pump; ast:pumpId ?pumpIdNew .
                ?plant a ast:Plant; ast:plantId ?plantId .
                FILTER(?pumpId != ?pumpIdNew)
            }");

        if changedPots != null then
            print("RECONFIG> Changed Pot(s) detected: repairing the model");

            while changedPots != null do
                PotDefectPump pot = changedPots.content;
                List<PotDefectPump> lx = changedPots;
                changedPots = changedPots.next;
                destroy(lx);

                print("RECONFIG> Changed pot to adjust: ");
                print("          Old Pump Id: " ++ pot.obj.pumpId);
                print("          New Pump Id: " ++ pot.pumpIdNew);

                pot.obj.pumpId = pot.pumpIdNew;
                destroy(pot);
            end

            print("RECONFIG> Pot(s) changed");
        end
    end



    /* Method to check if the plant of the Pot has changed */
    Unit checkPlantChanged()
        List<PotDefectPlant> changedPots = construct("
            PREFIX ast: <http://www.smolang.org/greenhouseDT#>
            SELECT DISTINCT ?obj ?plantIdNew {
                ?obj a prog:Pot ;
                    prog:Pot_shelfFloor ?shelfFloor ;
                    prog:Pot_potPosition ?potPosition ;
                    prog:Pot_pumpId ?pumpId ;
                    prog:Pot_plantId ?plantId .
                ?y a ast:Pot ;
                    ast:shelfFloor ?shelfFloor ;
                    ast:potPosition ?potPosition ;
                    ast:wateredBy ?pump ;
                    ast:hasPlant ?plant .
                ?pump a ast:Pump; ast:pumpId ?pumpId .
                ?plant a ast:Plant; ast:plantId ?plantIdNew .
                FILTER(?plantId != ?plantIdNew)
            }");

        if changedPots == null then print("RECONFIG> No plant changed on pots"); else
            print("RECONFIG> Changed Pot(s) detected: repairing the model");

            while changedPots != null do
                PotDefectPlant pot = changedPots.content;
                List<PotDefectPlant> lx = changedPots;
                changedPots = changedPots.next;
                destroy(lx);

                print("RECONFIG> Changed pot to adjust: ");
                print("          Old Plant Id: " ++ pot.obj.plantId);
                print("          New Plant Id: " ++ pot.plantIdNew);

                pot.obj.plantId = pot.plantIdNew;
                destroy(pot);
            end

            print("RECONFIG> Pot(s) changed");
        end
    end

    override Unit adaptBehaviour() skip; end
end