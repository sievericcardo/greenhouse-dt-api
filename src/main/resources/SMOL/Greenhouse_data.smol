abstract class Twin() end

class Sensor extends Twin (String sensorId, String sensorProperty)
end

class MoistureSensor extends Sensor (Double moisture)
end

class NutrientSensor extends Sensor (Double nutrient)
end

class LightSensor extends Sensor (Double lightIntensity)
end

class TemperatureHumiditySensor extends Sensor (Double temperature, Double humidity)
end

class HealthState extends Twin (context Plant plant, String name)
end

class Healthy extends HealthState ()
classifies "<ast:Healthy>";
    Unit printInfo()
        print("Plant is healthy.");
    end
end

class Unhealthy extends HealthState ()
classifies "<ast:Unhealthy>";
    Unit printInfo()
        print("Plant is unhealthy.");
    end
end

class Dead extends HealthState ()
classifies "<ast:Dead>";
    Unit printInfo()
        print("Plant is dead.");
    end
end

/**
 *  Models a physical plant. It is initially retrieved by
 *  the asset model, but it can be reconfigured by the program model.
 *  Each plant is associated with a pot.
 */
class Plant extends Twin (
    hidden String plantId,
    domain String familyName,
    domain Double moisture,
    domain Double ndvi,
    hidden HealthState healthState,
    domain String status
)
models "a ast:Plant ; domain:familyName %familyName ; domain:moisture %moisture ; domain:ndvi %ndvi";
    Unit printInfo()
        print("Plant: " ++ this.plantId ++ " has ideal moisture: " ++ this.idealMoisture ++ " and current moisture: " ++ this.moisture ++ ".");
    end

    Double getPotMoisture()
        String id = this.plantId ++ ".0";
        Double moisture = -1.0;
        List<Double> influxReturn = access(
            "from(bucket: \"GreenHouse\")
                |> range(start: -30d)
                |> filter(fn: (r) => r[\"_measurement\"] == \"ast:pot\")
                |> filter(fn: (r) => r[\"_field\"] == \"moisture\")
                |> filter(fn: (r) => r[\"plant_id\"] == \"%1\")
                |> keep(columns: [\"_value\"])
                |> last()",
            INFLUXDB("config_local.yml"),
            id);

        if influxReturn != null then
            moisture = influxReturn.content;
        end

        return moisture;
    end

    Unit getNdvi()
        String id = this.plantId ++ ".0";
        List<Double> influxReturn = access(
            "from(bucket: \"GreenHouse\")
                |> range(start: -30d)
                |> filter(fn: (r) => r[\"_measurement\"] == \"ast:plant\")
                |> filter(fn: (r) => r[\"_field\"] == \"ndvi\")
                |> filter(fn: (r) => r[\"plant_id\"] == \"%1\")
                |> keep(columns: [\"_value\"])
                |> last()",
        INFLUXDB("config_local.yml"),
        id);

        if influxReturn != null then
            this.ndvi = influxReturn.content;
        end
    end
end

class ThirstyPlant extends Plant ()
classifies "<ast:Thirsty>";
    Unit printInfo()
        print("Plant: " ++ this.plantId ++ " is thirsty.");
    end
end

// class OverwateredPlant extends Plant ()
// classifies "<ast:Overwatered>";
//     Unit printInfo()
//         print("Plant: " ++ this.plantId ++ " is overwatered.");
//     end
// end

class MoistPlant extends Plant ()
classifies "<ast:Moist>";
    Unit printInfo()
        print("Plant: " ++ this.plantId ++ " is moist.");
    end
end

/* Models a physical pot. It is initially retrieved by the asset model. Each pot is associated with a moisture sensor. */
class Pot extends Twin (
    String potId, 
    List<Plant> plants, 
    MoistureSensor moistureSensor, 
    NutrientSensor nutrientSensor.
    Pump pump
)
    List<Plant> getPlants()
        List<Plant> plants = access("
            PREFIX ast: <http://www.smolang.org/greenhouseDT#>
            SELECT ?obj {
                ?pot a ast:Pot ;
                     ast:potId %1 ;
                     ast:hasPlant ?plants .
                ?plants rdf:type ?t ;
                     ast:plantId ?plantId .
                ?t rdfs:subClassOf* ast:Plant .
                ?obj a prog:Plant ;
                     prog:Plant_plantId ?plantId .
            }", this.potId);

        if plants == null then
            print("No plants found in pot " ++ this.potId);
        else
            print("Plants found in pot " ++ this.potId);
        end

        return plants;
    end

    MoistureSensor getMoistureSensor()
        List<MoistureSensor> moistureSensors = access("
            PREFIX ast: <http://www.smolang.org/greenhouseDT#>
            SELECT ?obj {
                ?pot a ast:Pot ;
                     ast:potId %1 ;
                     ast:hasMoistureSensor ?moistureSensor .
                ?moistureSensor a ast:MoistureSensor ;
                     ast:sensorId ?sensorId .
                ?obj a prog:MoistureSensor ;
                     prog:MoistureSensor_sensorId ?sensorId .
            }", this.potId);

        if moistureSensors == null then
            print("No moisture sensor found in pot " ++ this.potId);
            return null;
        else
            print("Moisture sensor found in pot " ++ this.potId);
            return moistureSensors.content;
        end
    end

    NutrientSensor getNutrientSensor()
        List<NutrientSensor> nutrientSensors = access("
            PREFIX ast: <http://www.smolang.org/greenhouseDT#>
            SELECT ?obj {
                ?pot a ast:Pot ;
                     ast:potId %1 ;
                     ast:hasNutrientSensor ?nutrientSensor .
                ?nutrientSensor a ast:NutrientSensor ;
                     ast:sensorId ?sensorId .
                ?obj a prog:NutrientSensor ;
                     prog:NutrientSensor_sensorId ?sensorId .
            }", this.potId);

        if nutrientSensors == null then
            print("No nutrient sensor found in pot " ++ this.potId);
            return null;
        else
            print("Nutrient sensor found in pot " ++ this.potId);
            return nutrientSensors.content;
        end
    end

    Pump getPump()
        List<Pump> pumps = access("
            PREFIX ast: <http://www.smolang.org/greenhouseDT#>
            SELECT ?obj {
                ?pot a ast:Pot ;
                     ast:potId %1 ;
                     ast:hasPump ?pump .
                ?pump a ast:Pump ;
                     ast:pumpId ?pumpId .
                ?obj a prog:Pump ;
                     prog:Pump_pumpId ?pumpId .
            }", this.potId);

        if pumps == null then
            print("No pump found in pot " ++ this.potId);
            return null;
        else
            print("Pump found in pot " ++ this.potId);
            return pumps.content;
        end
    end
end

/*  Models a physical section of the greenhouse. It is initially retrieved by the asset model.
 *  Each section contains a list of pots.
 */
class Section extends Twin (String sectionId, List<Pot> pots)
    List<Pot> getPots()
        List<Pot> pots = access("
            PREFIX ast: <http://www.smolang.org/greenhouseDT#>
            SELECT ?obj {
                ?sections a ast:Section ;
                     ast:sectionId %1 ;
                     ast:hasPot ?pots .
                ?pots a ast:Pot ;
                     ast:potId ?potId .
                ?obj a ast:Pot ;
                     ast:potId ?potId .
            }", this.sectionId);

        if pots == null then
            print("No pots found in section " ++ this.sectionId);
        else
            print("Pots found in section " ++ this.sectionId);
        end

        return pots;
    end
end

/*  Models a physical pump. It is initially retrieved by the asset model. */
class Pump extends Twin (
    Int pumpChannel,
    String pumpId,
    domain String modelName,
    hidden String modelNameOut,
    domain Int pumpLifeTime,
    hidden Int pumpLifeTimeOut,
    domain Double temperature, 
    hidden Double temperatureOut
)
models "a ast:Pump ; domain:temperature %temperature ; domain:pumpLifeTime %pumpLifeTime ; domain:modelName %modelName";
end

class OperatingPump extends Pump ()
classifies "<ast:Operational>";
    Unit printInfo()
        print("Pump: " ++ this.pumpId ++ " is operational.");
    end
end

class MaintenancePump extends Pump (hidden Int powerOutput)
classifies "<ast:Maintenance>";
retrieves "SELECT DISTINCT ?powerOutput WHERE {
  %this a prog:Pump ;
    domain:models ?x .
  ?x domain:modelName ?modelName .
  ?pump a ast:Maintenance ;
    ast:powerOutput ?powerOutput ;
    ast:modelName ?model .
  FILTER (?model = ?modelName) .
}";
    Unit printInfo()
        print("Pump: " ++ this.pumpId ++ " is in maintenance. Set power to " ++ this.powerOutput ++ ".");
    end
end

class OverheatingPump extends Pump ()
classifies "<ast:Overheating>";
    Unit printInfo()
        print("Pump: " ++ this.pumpId ++ " is overheating.");
    end
end

class UnderheatingPump extends Pump ()
classifies "<ast:Underheating>";
    Unit printInfo()
        print("Pump: " ++ this.pumpId ++ " is underheating.");
    end
end

class WaterBucket extends Twin (
    String bucketId,
    Double waterLevel
) end

class GreenHouse extends Twin (
    String greenhouseId,
    List<Section> sections,
    List<WaterBucket> waterBuckets,
    LightSensor lightSensor,
    TemperatureHumiditySensor temperatureHumiditySensor,
)
    List<Section> getSections()
        List<Section> sections = access("
            PREFIX ast: <http://www.smolang.org/greenhouseDT#>
            SELECT ?obj {
                ?greenhouse a ast:GreenHouse ;
                             ast:greenhouseId %1 ;
                             ast:hasSection ?sections .
                ?sections a ast:Section ;
                          ast:sectionId ?sectionId .
                ?obj a prog:Section ;
                      prog:Section_sectionId ?sectionId .
            }", this.greenhouseId);

        if sections == null then
            print("No sections found in greenhouse " ++ this.greenhouseId);
        else
            print("Sections found in greenhouse " ++ this.greenhouseId);
        end

        return sections;
    end

    List<WaterBucket> getWaterBuckets()
        List<WaterBucket> waterBuckets = access("
            PREFIX ast: <http://www.smolang.org/greenhouseDT#>
            SELECT ?obj {
                ?greenhouse a ast:GreenHouse ;
                             ast:greenhouseId %1 ;
                             ast:hasWaterBucket ?waterBuckets .
                ?waterBuckets a ast:WaterBucket ;
                              ast:bucketId ?bucketId .
                ?obj a prog:WaterBucket ;
                      prog:WaterBucket_bucketId ?bucketId .
            }", this.greenhouseId);

        if waterBuckets == null then
            print("No water buckets found in greenhouse " ++ this.greenhouseId);
        else
            print("Water buckets found in greenhouse " ++ this.greenhouseId);
        end

        return waterBuckets;
    end

    LightSensor getLightSensor()
        List<LightSensor> lightSensors = access("
            PREFIX ast: <http://www.smolang.org/greenhouseDT#>
            SELECT ?obj {
                ?greenhouse a ast:GreenHouse ;
                             ast:greenhouseId %1 ;
                             ast:hasLightSensor ?lightSensor .
                ?lightSensor a ast:LightSensor ;
                             ast:sensorId ?sensorId .
                ?obj a prog:LightSensor ;
                      prog:LightSensor_sensorId ?sensorId .
            }", this.greenhouseId);

        if lightSensors == null then
            print("No light sensor found in greenhouse " ++ this.greenhouseId);
            return null;
        else
            print("Light sensor found in greenhouse " ++ this.greenhouseId);
            return lightSensors.content;
        end
    end

    TemperatureHumiditySensor getTemperatureHumiditySensor()
        List<TemperatureHumiditySensor> temperatureHumiditySensors = access("
            PREFIX ast: <http://www.smolang.org/greenhouseDT#>
            SELECT ?obj {
                ?greenhouse a ast:GreenHouse ;
                             ast:greenhouseId %1 ;
                             ast:hasTemperatureHumiditySensor ?temperatureHumiditySensor .
                ?temperatureHumiditySensor a ast:TemperatureHumiditySensor ;
                                            ast:sensorId ?sensorId .
                ?obj a prog:TemperatureHumiditySensor ;
                      prog:TemperatureHumiditySensor_sensorId ?sensorId .
            }", this.greenhouseId);

        if temperatureHumiditySensors == null then
            print("No temperature/humidity sensor found in greenhouse " ++ this.greenhouseId);
            return null;
        else
            print("Temperature/humidity sensor found in greenhouse " ++ this.greenhouseId);
            return temperatureHumiditySensors.content;
        end
    end
end



// NDVI START
/* Models the health state of a plant via NDVI */
// class HealthState extends Twin (String name, Double minNdvi, Double maxNdvi) end
// NDVI END

/* The following classes are used to store results of CONSTRUCT queries */
class PumpDefect (Pump obj, Int pumpChannelNew) end
class PumpDefectWater (Pump obj, Double temperatureNew) end
class OperatingPumpDefectWater (OperatingPump obj, Double temperatureNew) end
class MaintenancePumpDefectWater (MaintenancePump obj, Double temperatureNew) end
class OverheatingPumpDefectWater (OverheatingPump obj, Double temperatureNew) end
class UnderheatingPumpDefectWater (UnderheatingPump obj, Double temperatureNew) end
class PumpDefectLifeTime (Pump obj, Int pumpLifeTimeNew) end
class OperatingPumpDefectLifeTime (OperatingPump obj, Int pumpLifeTimeNew) end
class MaintenancePumpDefectLifeTime (MaintenancePump obj, Int pumpLifeTimeNew) end
class OverheatingPumpDefectLifeTime (OverheatingPump obj, Int pumpLifeTimeNew) end
class UnderheatingPumpDefectLifeTime (UnderheatingPump obj, Int pumpLifeTimeNew) end
class PlantDefect (Plant obj, Double idealMoistureNew) end
class PlantDefectStatus (Plant obj, String statusNew) end
class PotDefectShelf (Pot obj, String shelfFloorNew) end
class PotDefectPosition (Pot obj, String potPositionNew) end
class PotDefectPump (Pot obj, String pumpIdNew) end
class PotDefectPlant (Pot obj, String plantIdNew) end

/*
 *  The follow class is used to store results that are read from the simulation driver.
 *  Every instance is representing on decision to water a specific plant and pump.
 */
class Decision (String plantId, Int pumpChannel, String pumpId) end